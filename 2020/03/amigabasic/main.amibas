OPTION BASE 0

DIM SHARED n%     ' number of input lines
DIM SHARED count% ' return value of tree counter

filename$ = "Shared:input.txt"

PRINT TIME$; " reading file "; filename$

CountFileLines(filename$)

DIM SHARED lines$(n%)
ReadLines(filename$)

PRINT TIME$; " counting trees"

CALL CountTrees(3, 1)

solution1% = count%

product# = 1#
FOR i% = 1 TO 5
  READ right%, down%
  CALL CountTrees(right%, down%)
  product# = product# * CDBL(count%)
NEXT
PRINT TIME$; " done"
PRINT solution1%
PRINT USING "################"; product# 


' -------- sub routines --------

SUB CountTrees(right%, down%) STATIC:
  w% = LEN(lines$(0))
  count% = 0
  j% = 0
  FOR i% = 0 TO n% - 1 STEP down%
    char$ = MID$(lines$(i%), j% * right% MOD w% + 1, 1)
    j% = j% + 1
    IF char$ = "#" THEN count% = count% + 1
  NEXT
END SUB

SUB CountFileLines (filename$) STATIC
  ' required shared variable:
  '  n% -> returns number of lines in file

  OPEN filename$ FOR INPUT AS #1
  n% = 0
  WHILE EOF(1) = 0
    INPUT#1, content$
    n% = n% + 1
  WEND
  CLOSE #1
END SUB

SUB ReadLines (filename$) STATIC
  ' required shared variables:
  '  n%     -> number of lines to be read
  '  lines% -> returns lines

  OPEN filename$ FOR INPUT AS #1
  FOR i% = 0 TO n% - 1
    INPUT#1, lines$(i%)
  NEXT
  CLOSE #1
END SUB


' ---- slopes for part 2 ----

DATA 1, 1
DATA 3, 1
DATA 5, 1
DATA 7, 1
DATA 1, 2
